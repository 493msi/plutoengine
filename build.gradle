import org.gradle.internal.os.OperatingSystem

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

project.ext.versionYear = 20
project.ext.versionMajor = 2
project.ext.versionMinor = 0
project.ext.versionPatch = 0

project.ext.isPrerelease = true
project.ext.prereleaseName = "alpha"
project.ext.prerealeaseUpdate = 2

allprojects {
    apply plugin: 'java'

    group = "cz.tefek"
    version = isPrerelease ?
            "${versionYear}.${versionMajor}.${versionMinor}.${versionPatch}-${prereleaseName}.${prerealeaseUpdate}"
            :
            "${versionYear}.${versionMajor}.${versionMinor}.${versionPatch}"

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

subprojects {
    apply plugin: 'maven-publish'

    project.ext.lwjglVersion = "3.2.3"

    project.ext.jomlVersion = "1.9.25"
    project.ext.steamworks4jVersion = "1.8.0"
    project.ext.steamworks4jServerVersion = "1.8.0"

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    switch (OperatingSystem.current()) {
        case OperatingSystem.LINUX:
            project.ext.lwjglNatives = "natives-linux"
            break
        case OperatingSystem.MAC_OS:
            project.ext.lwjglNatives = "natives-macos"
            break
        case OperatingSystem.WINDOWS:
            project.ext.lwjglNatives = "natives-windows"
            break
    }

    repositories {
        mavenCentral()
    }
}
